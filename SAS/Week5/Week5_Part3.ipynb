{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "7eda2853-eee6-4b4b-a8fd-a1510536a106",
   "metadata": {},
   "source": [
    "### GWU STAT 4197/STAT 6197\n",
    "#### Week 5, Part 1: Controling and Managing SAS Data Sets"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f1bd8a59-2111-4d87-a673-bc6db2999496",
   "metadata": {},
   "source": [
    " ### Single_CPT_Download.sas\n",
    " \n",
    " TO DOWNLOAD, UNZIP AND RESTORE a SAS data set from only a single CPORT-created \n",
    " trnsposrt file only one at a time) from the MEPS website     \n",
    "\n",
    " Just create a macro variable for the data file you are interested in\n",
    " (%LET file = h206a;)   That's it! There is no macro program involved here. \n",
    "\n",
    " The output files are saved in the WORK folder. \n",
    " CAUTION: Downloaed unzipped/zipped/restored SAS data files will only exist \n",
    " in the current SAS session.\n",
    "\n",
    " Adjust the code as appropriate to save the files permanently.\n",
    "   "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6bddb811-5599-4b87-b472-43a480956914",
   "metadata": {},
   "outputs": [],
   "source": [
    "proc datasets library=work kill;run;quit;\n",
    "%LET file=h206a; \n",
    "%put &=file;\n",
    "/* use pathname function to get work library's path and store in macro variable wpath */\n",
    "%let wpath=%sysfunc(pathname(work));\n",
    "\n",
    "*** Task 1: Download a zipped SAS transport file from the MEPS web site;\n",
    "\n",
    "/* use wpath macro variable and append filename to it in filename statement */\n",
    "*options symbolgen mprint mlogic merror;\n",
    "Filename GoThere \"&wpath\\&file.ssp.zip\";\n",
    "proc http \n",
    "   url=\"https://meps.ahrq.gov/data_files/pufs/&file.ssp.zip\" \n",
    "   out=GoThere;\n",
    "run;\n",
    "/***********************************************************************\n",
    "* Acknowledgements:\n",
    "* https://blogs.sas.com/content/sasdummy/2015/05/11/using-filename-zip-to-unzip-and-read-data-files-in-sas/\n",
    "* Read the \"members\" (files) from the ZIP file\n",
    "***************************************************************************************************************/\n",
    "/*Assign a fileref wth the ZIP method */\n",
    "filename inzip zip \"&wpath\\&file.ssp.zip\";\n",
    " \n",
    "data contents(keep=memname);\n",
    " length memname $200;\n",
    " fid=dopen(\"inzip\");\n",
    " if fid=0 then\n",
    "  stop;\n",
    " memcount=dnum(fid);\n",
    " do i=1 to memcount;\n",
    "  memname=dread(fid,i);\n",
    "  output;\n",
    " end;\n",
    " rc=dclose(fid);\n",
    " call symputx ('memname', memname);\n",
    "run;\n",
    " %put &=memname;\n",
    "/* create a report of the ZIP contents */\n",
    "title \"Files in the ZIP file\";\n",
    "proc print data=contents noobs N;\n",
    "run;\n",
    " \n",
    "*** Task 2: Unzip the SAS transport data file (Member name in upper case);\n",
    "filename inzip zip \"&wpath\\&file.ssp.zip\" ;\n",
    "filename sit \"&wpath\\&memname\" ;\n",
    "/* hat tip: \"data _null_\" on SAS-L */\n",
    " data _null_; \n",
    "infile inzip(&memname) recfm=F lrecl=256 length=len_length eof=eof unbuf;\n",
    "file sit lrecl=256 recfm=N; \n",
    "input @; put _infile_ $varying256. len_length;\n",
    "return; \n",
    "eof: stop;\n",
    "run;\n",
    "\n",
    "*** Task 3: Restore the SAS data set from the PROC CPORT-created SAS transport file;\n",
    "filename cpt \"&wpath\\&memname\" ;\n",
    "proc cimport infile=cpt lib=work;\n",
    "run;\n",
    "\n",
    "/*********************************************************\n",
    "* Show the metadata for the SAS data set restored from the transport file (optional)               \n",
    "***********************************************************/\n",
    "proc sql;\n",
    "select memname,\n",
    "        nobs format =comma9.\n",
    "       ,nvar format =comma9.\n",
    "\t   ,DATEPART(crdate) format date9. as Date_created label='Creation Date'\n",
    "\t   ,TIMEPART(crdate) format timeampm. as Time_created label='Creation Time'\n",
    "from dictionary.tables\n",
    " where libname='WORK' and memname like upper(\"&file\");\n",
    " quit;\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "af2ab419-0471-4f6c-8389-77082c26c730",
   "metadata": {},
   "source": [
    "#### Single_XPT_Download.sas\n",
    "\n",
    "TO DOWNLOAD, UNZIP AND RESTORE a SAS data set from only a single XPORT-created \n",
    "trnsposrt file only one at a time) from the MEPS website     \n",
    "\n",
    " Just create a macro variable for the data file you are interested in\n",
    " (%LET file = h183;)   That's it! There is no macro program involved here. \n",
    "\n",
    " The output files are saved in the WORK folder. \n",
    " CAUTION: Downloaed unzipped/zipped/restored SAS data files will only exist \n",
    " in the current SAS session.\n",
    "\n",
    " Adjust the code as appropriate to save the files permanently.                 "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be01aef0-3c07-4a10-b0ff-91fd465c4721",
   "metadata": {},
   "outputs": [],
   "source": [
    "proc datasets library=work kill;run;quit;\n",
    "%LET file=h183; \n",
    "%put &=file;\n",
    "/* use pathname function to get work library's path and store in macro variable wpath */\n",
    "%let wpath=%sysfunc(pathname(work));\n",
    "\n",
    "*** Task 1: Download a zipped SAS transport file from the MEPS web site;\n",
    "\n",
    "/* use wpath macro variable and append filename to it in filename statement */\n",
    "*options symbolgen mprint mlogic merror;\n",
    "Filename GoThere \"&wpath\\&file.ssp.zip\";\n",
    "proc http \n",
    "   url=\"https://meps.ahrq.gov/data_files/pufs/&file.ssp.zip\" \n",
    "   out=GoThere;\n",
    "run;\n",
    "/***********************************************************************\n",
    "* Acknowledgements:\n",
    "* https://blogs.sas.com/content/sasdummy/2015/05/11/using-filename-zip-to-unzip-and-read-data-files-in-sas/\n",
    "* Read the \"members\" (files) from the ZIP file\n",
    "***************************************************************************************************************/\n",
    "/*Assign a fileref wth the ZIP method */\n",
    "filename inzip zip \"&wpath\\&file.ssp.zip\";\n",
    " \n",
    "data contents(keep=memname);\n",
    " length memname $200;\n",
    " fid=dopen(\"inzip\");\n",
    " if fid=0 then\n",
    "  stop;\n",
    " memcount=dnum(fid);\n",
    " do i=1 to memcount;\n",
    "  memname=dread(fid,i);\n",
    "  output;\n",
    " end;\n",
    " rc=dclose(fid);\n",
    " call symputx ('memname', memname);\n",
    "run;\n",
    " %put &=memname;\n",
    "/* create a report of the ZIP contents */\n",
    "title \"Files in the ZIP file\";\n",
    "proc print data=contents noobs N;\n",
    "run;\n",
    " \n",
    "*** Task 2: Unzip the SAS transport data file (Member name in upper case);\n",
    "filename inzip zip \"&wpath\\&file.ssp.zip\" ;\n",
    "filename sit \"&wpath\\&memname\" ;\n",
    "/* hat tip: \"data _null_\" on SAS-L */\n",
    " data _null_; \n",
    "infile inzip(&memname) recfm=F lrecl=256 length=len_length eof=eof unbuf;\n",
    "file sit lrecl=256 recfm=N; \n",
    "input @; put _infile_ $varying256. len_length;\n",
    "return; \n",
    "eof: stop;\n",
    "run;\n",
    "\n",
    "*** Task 3: Restore the SAS data set from the XPORT-engine-created SAS transport file;\n",
    "\n",
    "libname xpt xport \"&wpath\\&memname\";\n",
    " proc copy in=xpt out=work; \n",
    "run;\n",
    "\n",
    "/*********************************************************\n",
    "* Show the metadata for the SAS data set restored from the transport file (optional)               \n",
    "***********************************************************/\n",
    "proc sql;\n",
    "select memname,\n",
    "        nobs format =comma9.\n",
    "       ,nvar format =comma9.\n",
    "\t   ,DATEPART(crdate) format date9. as Date_created label='Creation Date'\n",
    "\t   ,TIMEPART(crdate) format timeampm. as Time_created label='Creation Time'\n",
    "from dictionary.tables\n",
    " where libname='WORK' and memname like upper(\"&file\");\n",
    " quit;\n",
    "\n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
